---
date: 2010-07-17 07:06:57
layout: post
title: A Method of Coming Clean
---
Since Perl has no inbuilt way of declaring if a subroutine is a function or a method, it's perfectly possible to mistakenly override a superclass method with an errant function import or accidentally call a function as a method call from outside the class it was declared in, violating encapsulation.  This is particularly problematic with generically named utility functions (or "language extensions") that are imported into your module's namespace from other modules.  In this blog entry I'm going to talk about what can be done to prevent this, and introduce you to the namespace::clean module that can automate this process.

<h2>An Example of the Problem</h2>

As way of example, let's write a simple class that looks up details about a user from their Facebook profile:

<pre><code>  package FacebookUser;
  use Moose;

  use LWP::Simple qw(get);
  use JSON::XS qw(decode_json);
  
  has api_access_token =&gt; ( isa =&gt; 'Str', is =&gt; 'ro', required =&gt; 1 );
  has username         =&gt; ( isa =&gt; 'Str', is =&gt; 'ro', required =&gt; 1 );
  has data             =&gt; ( isa =&gt; 'HashRef', is =&gt; 'ro', lazy_build =&gt; 1 );
  
  sub _build_data {
    my $self = shift;
    my $url = "https://graph.facebook.com/@{[ $self-&gt;username ]}?access_token=@{[ $self-&gt;api_access_token ]}";
    return decode_json(get($url));
  }
  
  sub get_firstname { return $_[0]-&gt;data-&gt;{first_name} }
  sub get_lastname  { return $_[0]-&gt;data-&gt;{first_name} }
  sub get_gender    { return $_[0]-&gt;data-&gt;{gender} }
  sub get_hometown  { return $_[0]-&gt;data-&gt;{hometown}{name} }</code></pre>

This is designed to be called like so:

<pre><code>  my $user = FacebookUser-&gt;new(
    api_access_token =&gt; $access_token,
    username =&gt; "2shortplanks",
  );
  say "Hello ".$user-&gt;get_firstname;</code></pre>

But what happens if someone calls it like so:

<pre><code>  my $user = FacebookUser-&gt;new(
    access_token =&gt; $access_token,
    username =&gt; "2shortplanks",
  );
  say "Hello ".$user-&gt;get('firstname');</code></pre>

Instead of a complaint about calling a non-existant <code>get</code> method we instead get the very cryptic error message:

<pre><code>  Can't use a FacebookUser object as a URI at /Library/Perl/5.10.0/HTTP/Request/Common.pm line 103</code></pre>

Why's this?  Because Perl tried to call the <code>get</code> function that we imported into our module from LWP::Simple as a method call, passing the invocant as the first argument instead of a URL. 

<h2>The obscured parent method problem</h2>

Bad error messages when we write stupid code are one thing.  Left over functions totally breaking our code in ways we don't expect is another.

Let's trying writing the same module using the Class::Accessor object system instead of using Moose.

<pre><code>  package FacebookUser;
  use base qw(Class::Accessor);

  # use Class::Accessor to create the api_access_token and username accessor methods
  __PACKAGE__-&gt;mk_accessors(qw(api_access_token username));

  use LWP::Simple qw(get);
  use JSON::XS qw(decode_json);

  sub data {
    my $self = shift;
    return $self-&gt;{data} ||= $self-&gt;_build_data;
  }

  sub _build_data {
    my $self = shift;
    my $url = "https://graph.facebook.com/@{[ $self-&gt;username ]}?access_token=@{[ $self-&gt;api_access_token ]}";
    return decode_json(get($url));
  }

  sub get_firstname { return $_[0]-&gt;data-&gt;{first_name} }
  sub get_lastname  { return $_[0]-&gt;data-&gt;{first_name} }
  sub get_gender    { return $_[0]-&gt;data-&gt;{gender} }
  sub get_hometown  { return $_[0]-&gt;data-&gt;{hometown}{name} }</code></pre>

This is designed to be called called like so:

<pre><code>  my $user = FacebookUser-&gt;new({
    access_token =&gt; $access_token,
    username =&gt; "2shortplanks",
  });
  say "Hello ".$user-&gt;get_firstname;</code></pre>

To our surprise, this doesn't work at all:

<pre><code>  Can't use a FacebookUser object as a URI at /Library/Perl/5.10.0/HTTP/Request/Common.pm line 103</code></pre>

That's the same error we got when we called <code>$user-&gt;get('firstname')</code> in our Moose example by mistake, but this time we're sure we're calling <code>$user-&gt;get_firstname</code>, so what gives?  How is the  <code>get</code> subroutine being called accidentally?  Let's run our example again with Carp::Always so we get a full stack trace out:

<pre><code>  bash$ perl -MCarp::Always example.pl 
  Can't use a FacebookUser object as a URI at /Library/Perl/5.10.0/HTTP/Request.pm line 70
  	HTTP::Request::uri('HTTP::Request=HASH(0x10091c5f8)', 'FacebookUser=HASH(0x10091c1f0)') called at /Library/Perl/5.10.0/HTTP/Request.pm line 16
  	HTTP::Request::new('HTTP::Request', 'GET', 'FacebookUser=HASH(0x10091c1f0)') called at /Library/Perl/5.10.0/HTTP/Request/Common.pm line 103
  	HTTP::Request::Common::_simple_req() called at /Library/Perl/5.10.0/HTTP/Request/Common.pm line 20
  	HTTP::Request::Common::GET('FacebookUser=HASH(0x10091c1f0)') called at /Library/Perl/5.10.0/LWP/UserAgent.pm line 386
  	LWP::UserAgent::get('LWP::UserAgent=HASH(0x10083c840)', 'FacebookUser=HASH(0x10091c1f0)') called at /Library/Perl/5.10.0/LWP/Simple.pm line 36
  	LWP::Simple::get('FacebookUser=HASH(0x10091c1f0)', 'username') called at /System/Library/Perl/Extras/5.10.0/Class/Accessor.pm line 393
  	Class::Accessor::__ANON__('FacebookUser=HASH(0x10091c1f0)') called at FacebookUser.pm line 18
  	FacebookUser::_build_data('FacebookUser=HASH(0x10091c1f0)') called at FacebookUser.pm line 13
  	FacebookUser::data('FacebookUser=HASH(0x10091c1f0)') called at FacebookUser.pm line 22
  	FacebookUser::get_firstname('FacebookUser=HASH(0x10091c1f0)') called at example.pl line 15</code></pre>
  
What's happened is that the <code>username</code> method that Class::Accessor created in our package is trying to call the <code>get</code> method in FacebookUser's Class::Accessor superclass but because we've imported a <code>get</code> function into FacebookUser that's being accidentally called as a method instead.  Ooops!

<h2>Solving this: Deleting the subroutine when done</h2>a

This problem can be solved in either code example by adding the following line to FacebookUser.pm to delete the <code>get</code> subroutine:

<pre><code>  delete $FacebookUser::{get};</code></pre>

Now a trying to call the get function as a method won't work, and we'll get a proper error message:

<pre><code>  say "Hello ".$user-&gt;get('firstname');
  Can't locate object method "get" via package "FacebookUser" at example.pl line 15</code></pre>

And with these modifications in both cases our <code>get_firstname</code> method call works as expected:

<pre><code>  say "Hello ".$user-&gt;get_firstname;
  Hello Mark</code></pre>

Those of you paying attention might be wondering how this possibly can still (if you'll pardon the pun) function.  After all, the the C<code>_build_data</code> method uses the <code>get</code> function to get the data from the facebook servers - and we just deleted the <code>get</code> function!

The answer to this conundrum relies on the fact that Perl is not an interpreted language, but a compiled one (one that is recompiled each time the program is started and has no executable or bytecode that is saved to disk.)  As such Perl has multiple phases of execution, with "compile time" (when the code is turned into bytecode) and "execution time" (when the code is actually running) happening at different times.  It's at the time the text it turned into bytecode that perl works out what function is what, and at compile time (after the symbol for the function has been deleted) that method calls occur and fail to find the errant <code>get</code>.

The same thing that means the following bit of code still works even though the <code>panic()</code> statement comes before the declaration of the function.

<pre><code>  panic();
  sub panic() { say "Don't panic Mr Mannering!"; return }</code></pre>

<h2>Doing this automatically</h2>

I'll be the first to admit that this syntax:

<pre><code>  delete $FacebookUser::{get};</code></pre>

Totally sucks.  It's ugly and completely unintelligible unless you happen to have learnt what it does beforehand.  Worse still, you need to remember to do it for each and every function you declare or import in your object and you might not even know all the functions that are imported if you rely on the default import list.  In other words, it involves a lot of bookkeeping and hence chances for mistakes.  Why don't we get the computer to automate this for us?

This is where the namespace::clean module from CPAN comes into use.  It's use is simple:

<pre><code>  use namespace::clean;</code></pre>

This deletes all function from your namespace that were declared or imported above the line that namespace::clean was <code>use</code>-ed on.  So, in our example we might like to do it after the lines where we've imported everything but before we start declaring methods and accessors:

<pre><code>  package FacebookUser;
  use base qw(Class::Accessor);

  use LWP::Simple qw(get);
  use JSON::XS qw(decode_json);

  use namespace::clean;

  # use Class::Accessor to create the api_access_token and username
  # accessor methods
  __PACKAGE__-&gt;mk_accessors(qw(api_access_token username));

  ...</code></pre>

Or with Moose:

<pre><code>  package FacebookUser;
  use Moose;

  use LWP::Simple qw(get);
  use JSON::XS qw(decode_json);
  
  use namespace::clean -except =&gt; 'meta';
  
  ...</code></pre>

In the later example we've told namespace::clean to keep the <code>meta</code> subroutine that <code>use Moose</code> imported for us - it's an important part of our object's framework.

<h2>Conclusion</h2>

The confusion between functions and methods in Perl is one that can cause a fair number of headaches for those not paying very close attention, and often requires us to have too much knowledge of the inner workings of our parent classes.  We can handle this problem manually, or the namespace::clean module on CPAN can help alleviate this pain.
